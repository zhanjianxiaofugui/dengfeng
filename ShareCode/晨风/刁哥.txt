[@main]
#IF
large <$HFCOUNT> 2
CheckLevelEx > 57
#ACT

goto @转生系统初始化
#ELSEACT
MESSAGEBOX 旁边的是我妹妹，颜值很高，身材很好，但是跟你有什么关系呢？？
[@转生系统初始化]
#IF
TRUE
#Act
MOV N$下一级转生等级 <$HUMAN(转生等级)>
INC N$下一级转生等级 1
ReadConfigFileItem ..\QuestDiary\配置文件\转生配置.ini  <$HUMAN(转生等级)>  属性加成  S$当前属性加成
SPLIT <$STR(S$当前属性加成)> | S$当前转生属性加成

ReadConfigFileItem ..\QuestDiary\配置文件\转生配置.ini <$STR(N$下一级转生等级)> 属性加成 S$属性加成
ReadConfigFileItem ..\QuestDiary\配置文件\转生配置.ini <$STR(N$下一级转生等级)> 消耗 S$消耗
ReadConfigFileItem ..\QuestDiary\配置文件\转生配置.ini <$STR(N$下一级转生等级)> 概率 S$概率
ReadConfigFileItem ..\QuestDiary\配置文件\转生配置.ini <$STR(N$下一级转生等级)> 保底次数 S$保底次数
ReadConfigFileItem ..\QuestDiary\配置文件\转生配置.ini <$STR(N$下一级转生等级)> 必失败次数 S$必失败次数
ReadConfigFileItem ..\QuestDiary\配置文件\转生配置.ini <$STR(N$下一级转生等级)> 最低等级 S$最低等级
MOV N$转生后开放等级 <$STR(S$最低等级)>
INC N$转生后开放等级 1
SPLIT <$STR(S$属性加成)> | S$转生属性加成
SPLIT <$STR(S$消耗)> | S$转生消耗
MOV N$转生概率 <$STR(S$概率)>
MOV N$转生保底次数 <$STR(S$保底次数)>
MOV N$转生必失败次数 <$STR(S$必失败次数)>
GOTO @平台入口

[@平台入口]
#IF
EQUAL $CLIENTFLAG 1
#ACT
;Calcvar HUMAN 转生等级 = 1
;Savevar HUMAN 转生等级 
GOTO @PC界面
#IF
!EQUAL $CLIENTFLAG 1
#ACT
;Calcvar HUMAN 转生等级 = 1
;Savevar HUMAN 转生等级 
GOTO @手游界面

[@PC界面] NPC(60351)
#if
true
#say
<@@!BTN 3,63206,3,63206,3,63217,800,-25,关闭/@EXIT>\
<offx=|280|y=|-5|fontsize=|20||font=|0|><color=8552a1  您的转生等级：> <color=8552a1 <$HUMAN(转生等级)>> <color=8552a1 转> <|off>\
;----特效
;<!SPIC 图包编号,图片在图包中的位置,图片张数,间隔(毫秒),X,Y,MODE,Num>
<@@!SPIC 3,65200,29,100,175,-80,3,0>\

;---字体底色
;<offx=|10|y=|-50|><@@!PIC2 3,64815,0,0><|off>\
<offx=|120|y=|50|><@@!PIC2 3,64011,0,0><|off>\
<offx=|550|y=|-25|><@@!PIC2 3,64011,0,0><|off>\
;----美术字
<offx=|120|y=|-30|><@@!PIC2 3,33051,0,0><|off>\
<offx=|550|y=|-37|><@@!PIC2 3,33052,0,0><|off>\
;----属性值
<offx=|100|y=|20|fontsize=|18||font=|0|><color=d71345  攻击 + <$STR(S$当前转生属性加成_0)>（<$STR(S$当前转生属性加成_1)>转）><|off>\
<offx=|100|y=|30|fontsize=|18||font=|0|><color=d71345  魔法 + <$STR(S$当前转生属性加成_0)>（<$STR(S$当前转生属性加成_1)>转）><|off>\
<offx=|100|y=|30|fontsize=|18||font=|0|><color=d71345  道术 + <$STR(S$当前转生属性加成_0)>（<$STR(S$当前转生属性加成_1)>转）><|off>\
<offx=|100|y=|30|fontsize=|18||font=|0|><color=d71345  物防 + <$STR(S$当前转生属性加成_0)>（<$STR(S$当前转生属性加成_1)>转）><|off>\
<offx=|100|y=|30|fontsize=|18||font=|0|><color=d71345  魔防 + <$STR(S$当前转生属性加成_0)>（<$STR(S$当前转生属性加成_1)>转）><|off>\

<offx=|550|y=|-205|fontsize=|18||font=|0|><color=d71345  攻击 + <$STR(S$转生属性加成_0)>（<$STR(S$转生属性加成_1)>转）><|off>\
<offx=|550|y=|-165|fontsize=|18||font=|0|><color=d71345  魔法 + <$STR(S$转生属性加成_0)>（<$STR(S$转生属性加成_1)>转）><|off>\
<offx=|550|y=|-118|fontsize=|18||font=|0|><color=d71345  道术 + <$STR(S$转生属性加成_0)>（<$STR(S$转生属性加成_1)>转）><|off>\
<offx=|550|y=|-68|fontsize=|18||font=|0|><color=d71345  物防 + <$STR(S$转生属性加成_0)>（<$STR(S$转生属性加成_1)>转）><|off>\
<offx=|550|y=|-18|fontsize=|18||font=|0|><color=d71345  魔防 + <$STR(S$转生属性加成_0)>（<$STR(S$转生属性加成_1)>转）><|off>\

<offx=|280|y=|30|fontsize=|18||font=|0|><color=d71345  转生成功概率:<$STR(N$转生概率)>%><|off>\
;<$STR(N$转生保底次数)>

;----保底描述
#if
Large <$STR(N$转生保底次数)> 0
#act
#say
<offx=|450|y=|-17|fontsize=|18||font=|0|><color=00ff00 （<$STR(N$转生保底次数)>）次保底><|off>\

#if
true
#SAY
<offx=|600|y=|-17|fontsize=|18||font=|0|><color=00ff00 等级开放至<$STR(N$转生后开放等级)>><|off>\

;----消耗
<offx=|280|y=|20|><@@!PIC2 3,64815,0,0><|off>\
<offx=|230|y=|-35|fontsize=|18||font=|0|><color=d71345 消耗：><|off>\

<offx=|330|y=|-50|><@@!PIC2 3,60316,0,0><|off>\
<offx=|435|y=|-50|><@@!PIC2 3,60316,0,0><|off>\
<offx=|391|y=|-38|fontsize=|18||font=|0|><color=ffd400  ╋><|off>\

<offx=|340|y=|-38|></@@!PIC <$STR(S$转生消耗_0)>,1,0,0> <|off>\
<offx=|445|y=|-38|></@@!PIC <$STR(S$转生消耗_2)>,1,0,0> <|off>\
<offx=|370|y=|-10|fontsize=|12||font=|0|><color=fffffb  <$STR(S$转生消耗_1)>><|off>\
<offx=|475|y=|-15|fontsize=|12||font=|0|><color=fffffb  <$STR(S$转生消耗_3)>><|off>\
;----按钮
<@@!BTN 3,65012,3,65013,3,65013,340,10,/@开始转生>\
<offx=|370|y=|-30|fontsize=|20||font=|0|><color=00ff00 开始转生><|off>\

[@手游界面]
#if
TRUE
#say
<Img|children={0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15}|a=0|x=200.0|y=100.0|width=700|height=445|scale9l=10|reset=1|scale9r=10|bg=1|move=0|scale9t=10|scale9b=10|img=public/zhuansheng/bg_zhsh.png|esc=1>
<Button|id=0|x=653.0|y=-5.0|nimg=public/025.png|color=255|scale=1|link=@exit>
<Text|id=1|a=0|x=270.0|y=30.0|tipsx=10|tipsy=80|color=253|size=18|text=您的转生等级：<$HUMAN(转生等级)>转>

<Frames|id=2|x=163.0|y=-35.0|count=30|suffix=.png|speed=10|prefix=public/tianyuanxinfa/dz/1_|loop=-1|scale=0.8>

<Img|id=3|children={1a,2a,3a,4a,5a,6a,7a}|x=80.0|y=80.0|scale=1|img=public/zhuansheng/zs_current.png|esc=0>
<RText|id=1a|x=27|y=50.0|color=255|size=18|text=<攻击+<$STR(S$当前转生属性加成_0)>-<$STR(S$当前转生属性加成_1)>/FCOLOR=249>>
<RText|id=2a|x=27.0|y=80.0|color=255|size=18|text=<魔法+<$STR(S$当前转生属性加成_0)>-<$STR(S$当前转生属性加成_1)>/FCOLOR=249>>
<RText|id=3a|x=27.0|y=110.0|color=255|size=18|text=<道术+<$STR(S$当前转生属性加成_0)>-<$STR(S$当前转生属性加成_1)>/FCOLOR=249>>
<RText|id=4a|x=27.0|y=140.0|color=255|size=18|text=<物防+<$STR(S$当前转生属性加成_0)>-<$STR(S$当前转生属性加成_1)>/FCOLOR=249>>
<RText|id=5a|x=27.0|y=170.0|color=255|size=18|text=<魔防+<$STR(S$当前转生属性加成_0)>-<$STR(S$当前转生属性加成_1)>/FCOLOR=249>>
<Img|id=6a|x=-7.0|y=3.0|width=150|height=33|img=public/zhuansheng/bg_meirlb_03.png|scale=1|esc=0>

<Img|id=4|x=270.0|y=30.0|width=150|height=25|img=public/zhuansheng/bg_meirlb_03.png|scale=1|esc=0>

<Img|id=5|children={1b,2b,3b,4b,5b,6b,7b}|x=500.0|y=80.0|scale=1|img=public/zhuansheng/zs_netxt.png|esc=0>
<RText|id=1b|x=27.0|y=50.0|color=255|size=18|text=<攻击+<$STR(S$转生属性加成_0)>-<$STR(S$转生属性加成_1)>/FCOLOR=249>>
<RText|id=2b|x=27.0|y=80.0|color=255|size=18|text=<魔法+<$STR(S$转生属性加成_0)>-<$STR(S$转生属性加成_1)>/FCOLOR=249>>
<RText|id=3b|x=27.0|y=110.0|color=255|size=18|text=<道术+<$STR(S$转生属性加成_0)>-<$STR(S$转生属性加成_1)>/FCOLOR=249>>
<RText|id=4b|x=27.0|y=140.0|color=255|size=18|text=<物防+<$STR(S$转生属性加成_0)>-<$STR(S$转生属性加成_1)>/FCOLOR=249>>
<RText|id=5b|x=27.0|y=170.0|color=255|size=18|text=<魔防+<$STR(S$转生属性加成_0)>-<$STR(S$转生属性加成_1)>/FCOLOR=249>>
<Img|id=6b|x=-7.0|y=3.0|width=150|height=33|img=public/zhuansheng/bg_meirlb_03.png|scale=1|esc=0>

<RText|id=6|x=250.0|y=280.0|color=255|size=18|text=<转生成功概率：<$STR(N$转生概率)>%/FCOLOR=249>>
<RText|id=13|x=510.0|y=280.0|color=250|size=18|text=<等级开放至<$STR(N$转生后开放等级)>级/FCOLOR=250>>
;<RText|id=7|x=425.0|y=280.0|color=255|size=18|text=<,<$STR(N$转生保底次数)>次保底/FCOLOR=249>>

#IF
Large <$STR(N$转生保底次数)> 0
#say
<RText|id=7|x=415.0|y=280.0|color=255|size=18|text=<(<$STR(N$转生保底次数)>次保底)/FCOLOR=250>>
#if
SMALL <$HUMAN(转生等级)> 9
#say
<RText|id=8|x=200.0|y=338.0|color=255|size=18|text=<消耗：/FCOLOR=249>>
<Img|id=9|children={9c}|x=260.0|y=320.0|scale=1|img=public/fuli/w.png|esc=0>
<ItemShow|id=9c|x=0.0|y=0.0|width=70|height=70|itemid=2045|itemcount=<$STR(S$转生消耗_1)>|bgtype=1|showtips=1|name=<$STR(S$转生消耗_0)>>
<Img|id=10|children={10d}|x=360.0|y=320.0|scale=1|img=public/fuli/w.png|esc=0>
<ItemShow|id=10d|x=0.0|y=0.0|width=70|height=70|itemid=2045|itemcount=<$STR(S$转生消耗_3)>|bgtype=1|showtips=1|name=<$STR(S$转生消耗_2)>>

<RText|id=11|x=338.0|y=338.0|size=16|color=253|text=<╋/FCOLOR=243>>
<Button|id=12|x=290.0|y=400.0|width=100|height=35|nimg=public/zhuansheng/zs_an1.png|color=255|link=@开始转生>
#ELSESAY
<Button|id=12|x=290.0|y=400.0|width=100|height=35|nimg=public/zhuansheng/zs_an2.png|color=255|link=@开始转生>

[@开始转生]
#if
CheckLevelEx < <$STR(S$最低等级)>
#Act
sendmsg 15 你的等级不足{<$STR(S$最低等级)>}！！！
break



#if
large <$HUMAN(转生等级)> 8
#Act
sendmsg 15 转生等级已满！！！
break
#if
!checkitem <$STR(S$转生消耗_0)> <$STR(S$转生消耗_1)> 
#Act
sendmsg 19 [提示]：你的材料或元宝不够！
Break
#if
Equal <$STR(S$转生消耗_2)> 元宝
checkmoney <$STR(S$转生消耗_2)> < <$STR(S$转生消耗_3)> 
#Act
sendmsg 19 [提示]：你的材料或元宝不够！
Break
#if
!Equal <$STR(S$转生消耗_2)> 元宝
!checkitem <$STR(S$转生消耗_2)> <$STR(S$转生消耗_3)> 
#Act
sendmsg 19 [提示]：你的材料或元宝不够！
Break

#IF
Equal <$STR(S$转生消耗_2)> 元宝
!CHECKMONEY 绑定元宝 < <$STR(S$转生消耗_3)> 
#ACT
MONEY 绑定元宝 - <$STR(S$转生消耗_3)> 
#ELSEACT
MOV N$剩余 <$STR(S$转生消耗_3)>
DEC N$剩余 <$BINDGAMEGOLD>
MONEY 绑定元宝 - <$BINDGAMEGOLD>
MONEY 元宝 - <$STR(N$剩余)>

#IF
!Equal <$STR(S$转生消耗_2)> 元宝
#ACT
TAKE <$STR(S$转生消耗_2)> <$STR(S$转生消耗_3)>

#IF
true
#ACT
TAKE <$STR(S$转生消耗_0)> <$STR(S$转生消耗_1)>
MOV N$转生失败次数 <$HUMAN(转生失败次数)>
MOV N$TEMP <$STR(N$转生保底次数)>
DEC N$TEMP 1
;----------------先判断保底
#IF
Small N$转生失败次数 <$STR(N$转生必失败次数)>
#ACT
Calcvar HUMAN 转生失败次数 + 1
Savevar HUMAN 转生失败次数
SENDMSG 15 很遗憾，转生失败，再接再厉！
SENDMSG 6 很遗憾，转生失败，再接再厉！
#CALL [\封号系统\在线封号.txt] @临时封号
#CALL [\封号系统\封号系统.txt] @绿色封号

goto @转生系统初始化
Break

#IF
true
#ACT
MOV N$TEMP <$STR(N$转生保底次数)>
DEC N$TEMP 1
#IF
equal N$转生失败次数 <$STR(N$TEMP)>
#ACT
Calcvar HUMAN 转生等级 + 1
Savevar HUMAN 转生等级
Calcvar HUMAN 转生失败次数 = 0
Savevar HUMAN 转生失败次数
RenewLevel 1 0 0
SENDMSG 15 恭喜您，转生成功，开放新的等级！
SENDMSG 6 恭喜您，转生成功，开放新的等级！
#CALL [\封号系统\在线封号.txt] @临时封号
#CALL [\封号系统\封号系统.txt] @绿色封号

goto @转生系统初始化
Break

;----------------开始随机
#IF
random <$STR(N$转生概率)> 100
#ACT
Calcvar HUMAN 转生等级 + 1
Savevar HUMAN 转生等级
Calcvar HUMAN 转生失败次数 = 0
Savevar HUMAN 转生失败次数
RenewLevel 1 0 0
SENDMSG 15 恭喜您，转生成功，开放新的等级！
SENDMSG 6 恭喜您，转生成功，开放新的等级
#CALL [\封号系统\在线封号.txt] @临时封号
#CALL [\封号系统\封号系统.txt] @绿色封号

goto @转生系统初始化
Break
#ELSEACT
Calcvar HUMAN 转生失败次数 + 1
Savevar HUMAN 转生失败次数
SENDMSG 15 很遗憾，转生失败，再接再厉！
SENDMSG 6 很遗憾，转生失败，再接再厉！
#CALL [\封号系统\在线封号.txt] @临时封号
#CALL [\封号系统\封号系统.txt] @绿色封号

goto @转生系统初始化
Break
